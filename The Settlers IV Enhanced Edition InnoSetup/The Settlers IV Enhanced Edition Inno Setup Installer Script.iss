; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "The Settlers IV Enhanced Edition"
#define MyAppGroup "The Settlers IV"
#define MyAppVersion "1.5.1.1200"
#define MyAppPublisher "Ortner MEDIA"
#define MyAppURL "https://www.zocker-lounge.com/s4-enhanced-edition"
#define MyAppCopyright "© 2019-2022 Ortner MEDIA"

#include "CodeDependencies.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId= {{74780053-A21A-43F0-9B91-9208F72095E9}
AppName={#MyAppName}
VersionInfoCopyright={#MyAppCopyright}
VersionInfoVersion={#MyAppVersion}
VersionInfoProductVersion={#MyAppVersion}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppGroup}
OutputDir=Output
OutputBaseFilename=TheSettlersIVEnhancedEditionSetup
WizardStyle=modern        
ShowLanguageDialog=auto
ArchitecturesInstallIn64BitMode=x64
SetupIconFile=Inno\S4EE_ICON.ico
UninstallDisplayIcon={app}\The Settlers IV Enhanced Edition.EXE
WizardImageFile=Inno\S4EE_Side.bmp
WizardSmallImageFile=Inno\S4EE_ICON.bmp   
DisableWelcomePage=no
InfoBeforeFile=Inno\ReadMe.txt
DisableDirPage=yes
DisableProgramGroupPage=yes
;Compression=lzma2/ultra64
PrivilegesRequired=admin


[Dirs]
Name: "{app}" ; Permissions: users-modify

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl" ; 
Name: "german"; MessagesFile: "compiler:Languages\German.isl"; InfoBeforeFile:"Inno\ReadMe.de-DE.txt";

[Files]
Source: "Publish\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\The Settlers IV Enhanced Edition"; Filename: "{app}\The Settlers IV Enhanced Edition.EXE"; WorkingDir: "{app}";
Name: "{commondesktop}\The Settlers IV Enhanced Edition"; Filename: "{app}\The Settlers IV Enhanced Edition.EXE"; WorkingDir: "{app}"; Tasks: desktopicon

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; 

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[Run]
;Filename: "{app}\The Settlers IV Enhanced Edition.EXE"; Flags: nowait postinstall hidewizard; Description: "{cm:LaunchProgram, {#MyAppName}}"

[UninstallRun]
Filename: "{app}\The Settlers IV Enhanced Edition.EXE"; Parameters: "/SilentUninstall"; RunOnceId: "TSIVEEUN"

[Files]
Source: "Inno\netcorecheck.exe"; Flags: dontcopy noencryption
Source: "Inno\netcorecheck_x64.exe"; Flags: dontcopy noencryption

[Code]
function InitializeSetup: Boolean;
begin
  //x86
  Dependency_ForceX86 := True; 
  Dependency_AddVC2015To2022;
  Dependency_ForceX86 := False;
  //x64
  Dependency_AddVC2015To2022;
  //Generic
  Dependency_AddDotNet48;
  Dependency_AddDotNet60Desktop;
  Dependency_AddWebView2;
  Result := True;
end;




